# fob-ruby build automation

default: build

build:
    cargo build --package fob-ruby
    @just _link-bundle debug

build-release:
    cargo build --release --package fob-ruby
    @just _link-bundle release

# Create .bundle symlink for macOS (Ruby requires .bundle extension)
_link-bundle mode:
    #!/usr/bin/env bash
    if [[ "$OSTYPE" == "darwin"* ]]; then
        ln -sf "{{justfile_directory()}}/../../target/{{mode}}/libfob_ruby.dylib" \
               "{{justfile_directory()}}/../../target/{{mode}}/fob_ruby.bundle"
        echo "Created fob_ruby.bundle symlink"
    fi

example: build
    cd {{justfile_directory()}}/../../examples/ruby/fob-simple && \
    /opt/homebrew/opt/ruby/bin/ruby bundler.rb

example-inline: build
    cd {{justfile_directory()}}/../../examples/ruby/fob-simple && \
    /opt/homebrew/opt/ruby/bin/ruby inline_content.rb

clean:
    cargo clean --package fob-ruby
    rm -f {{justfile_directory()}}/../../target/debug/fob_ruby.bundle
    rm -f {{justfile_directory()}}/../../target/release/fob_ruby.bundle

check:
    cargo check --package fob-ruby

clippy:
    cargo clippy --package fob-ruby
